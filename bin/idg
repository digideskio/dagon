#!/usr/bin/env dagon

require("readline")

trap("INT") ->
  exit

Idg:
  read-line:
    Readline.readline("idg:> ", true)

  read-next-line:
    Readline.readline("idg:..", true)

  start:
    lines: []
    line: false

    while true
      if lines.any?
        line: read-next-line
      else
        line: read-line

      lines.push(line)

      begin ->
        if line = false
          if lines.length > 1
            result: binding-eval(lines.join("\n"), TOP_LEVEL_BINDING)
            print("=> ")
            puts(result.inspect)
          else
            false
          exit
        else
          not-expecting-more: !line.strip.end-with?("->") && !line.strip.end-with?(":")
          if !line.start-with?("  ") && not-expecting-more
            result: binding-eval(lines.join("\n"), TOP_LEVEL_BINDING)
            print("=> ")
            puts(result.inspect)
            lines: []
          else
            false
      rescue ->(e)
        lines: []
        puts("#{e.class}: #{e.message}")

idg: Idg()
idg.start()

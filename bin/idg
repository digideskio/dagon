#!/usr/bin/env dagon

require("readline")

trap("INT") ->
  exit

Idg:
  read-line:
    Readline.readline("idg:> ", true)

  read-next-line:
    Readline.readline("idg:..", true)

  start:
    lines: []
    line: false

    while true
      if lines.any?
        line: read-next-line
      else
        line: read-line

      lines.push(line)

      begin ->
        if line = false
          if lines.length > 1
            result: eval(lines.join("\n"))
            print("=> ")
            puts(result.inspect)
          else
            false
          exit
        else
          not-expecting-more: !line.contains?("->") && !line.contains?(":")
          if !line.start-with?("  ") && not-expecting-more
            result: eval(lines.join("\n"))
            print("=> ")
            puts(result.inspect)
            lines: []
          else
            false
      rescue ->(e)
        lines: []
        puts(e.message)

idg: Idg()
idg.start()
